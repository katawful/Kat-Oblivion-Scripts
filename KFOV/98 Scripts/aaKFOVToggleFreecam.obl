scn aaKFOVToggleFreecam

; this toggles freecam

; event parameters
int iKey
int iAction

begin function {iKey, iAction}
	if MenuMode
		return
	endif
	; add filter since OnKey event handler is broken in 22.4
	; iKey must be our toggle key
	; iAction must be our action key
	if iKey == aaKFOV.ToggleFreecamKey && iAction == 0
		if aaKFOV.iFreecam == 0
			let aaKFOV.iPreviousFOV := FOV3rd
			if IsThirdPerson == 1 ; we in third person
				let aaKFOV.iCurrentPerspective := 3 ; in third
			elseif IsThirdPerson == 0
				let aaKFOV.iCurrentPerspective := 1 ; in first
			endif
			ToggleFirstPerson 0 ; toggle to third person
			con_TFC ; toggle freecam
			let aaKFOV.iFreecam := 1
		
		elseif aaKFOV.iFreecam == 1
			if aaKFOV.iCurrentPerspective == 3 ; was in third
				ToggleFirstPerson 0 ; go to third person
			elseif aaKFOV.iCurrentPerspective == 1
				ToggleFirstPerson 1 ; go to first person
			endif
			let FOV3rd := aaKFOV.iPreviousFOV
			con_TFC
			let aaKFOV.iFreecam := 0
		endif
	endif
end