scn aaKFOVIKeyEvent

; this starts the weather mode

int iKey
int iAction
ref temp


begin function {iKey, iAction}
	if MenuMode
		return
	endif
	; call each weather array
	let aaKFOV.aWeather := ar_Construct Array
	call aaKFOVArrayWVanilla		; purely vanilla weathers
	
	if (IsModLoaded "NAO.esp" && aaKFOV.EnableNAOWeathers == 1)
		call aaKFOVWArrayNAO
	endif

	if aaKFOV.iWeatherMode == 0 && aaKFOV.iFOVMode != 1 ; entering weather mode but fov isn't active
		call aaKFOVGetLastWeather
		; enable event handler for weather mode
		; since this event is broken, the parameter filters don't matter
		SetEventHandler "OnKeyEvent" aaKFOVEqualsKeyEvent first::13 second::0
		SetEventHandler "OnKeyEvent" aaKFOVMinusKeyEvent first::12 second::0
		Message "Entering Weather mode, disabling player controls for convenience"
		DisablePlayerControls
		set aaKFOV.iWeatherMode to 1
	elseif aaKFOV.iWeatherMode == 1 ; leaving weather mode
		RemoveEventHandler "OnKeyEvent" aaKFOVEqualsKeyEvent
		RemoveEventHandler "OnKeyEvent" aaKFOVMinusKeyEvent
		call aaKFOVRestoreWeather
		Message "Leaving Weather mode"
		EnablePlayerControls
		set aaKFOV.iWeatherMode to 0
	endif
end