scn DynamicLevelAlchemyStation

;-------------------------------------------------- 
; By KatsAwful/Kat, this script is available on the Nexus and my github repository
; This script is free to use with credit to me KatsAwful or Kat. Messaging me on Nexus is 
; appreciatated but not required
; https://github.com/katawful/Kat-Oblivion-Scripts
; This script is for a dynamic level alchemy station. It provides the respective level alchemy
; apparatus. It does not determine apparatus based upon the items you have with you.
;--------------------------------------------------

; variables for skill level
; they don't need to be integers I just like using them
int Nov		; novice
int App		; apprentice
int Jor		; journeyman
int Exr		; expert
int Mas		; master

int onGame	; variable to stop script from running in GameMode
int alcLvl	; variable for alchemy level to run during OnActivate

Begin OnActivate

set onGame to 1 	; set this to 1 since we are running the script

; set the alchemy level variables to 0
set Nov to 0
set App to 0
set Jor to 0
set Exr to 0
set Mas to 0

let alcLvl := PlayerREF.GetActorValue Alchemy	; variable to make the following section neat

; this section determines our alchemy level, adds the respective items, and equips an alchemy item for use
If alcLvl <= 24 			; novice
	PlayerREF.AddItemNS Alembic 1
	PlayerREF.AddItemNS Calcinator 1
	PlayerREF.AddItemNS Retort 1
	PlayerREF.AddItemNS MortarPestle 1
	set Nov to 1			; this is ordered this way so the var is set while in OnActivate
	PlayerREF.EquipItemSilent MortarPestle	; it doesn't matter which one we equip
ElseIf alcLvl >= 25 && alcLvl <= 49
	PlayerREF.AddItemNS AlembicApprentice 1
	PlayerREF.AddItemNS CalcinatorApprentice 1
	PlayerREF.AddItemNS RetortApprentice 1
	PlayerREF.AddItemNS MortarPestleApprentice 1
	set App to 1			; apprentice
	PlayerREF.EquipItemSilent MortarPestleApprentice
ElseIf alcLvl >= 50 && alcLvl <= 74	; journeyman
	PlayerREF.AddItemNS AlembicJourneyman 1
	PlayerREF.AddItemNS CalcinatorJourneyman 1
	PlayerREF.AddItemNS RetortJourneyman 1
	PlayerREF.AddItemNS MortarPestleJourneyman 1
	set Jor to 1
	PlayerREF.EquipItemSilent MortarPestleJourneyman
ElseIf alcLvl >= 75 && alcLvl <= 99	; expert
	PlayerREF.AddItemNS AlembicExpert 1
	PlayerREF.AddItemNS CalcinatorExpert 1
	PlayerREF.AddItemNS RetortExpert 1
	PlayerREF.AddItemNS MortarPestleExpert 1
	set Exr to 1
	PlayerREF.EquipItemSilent MortarPestleExpert
ElseIf alcLvl >= 100			; master
	PlayerREF.AddItemNS AlembicMaster 1
	PlayerREF.AddItemNS CalcinatorMaster 1
	PlayerREF.AddItemNS RetortMaster 1
	PlayerREF.AddItemNS MortarPestleMaster 1
	set Mas to 1
	PlayerREF.EquipItemSilent MortarPestleMaster
EndIf
End

; This following block needs to be below OnActivate since Oblivion will stop the script since 
; we have a return function to stop the script from always running while GameMode is active.
; Oblivion doesn't treat each block as unique and evaluates them top to bottom as a whole,
; this isn't standard behavior for other languages such as C (which this is mildly based off of).

Begin GameMode
If onGame == 0
	Return		; stops this script from running throughout the game
EndIf
; this section removes all the items we added based on our alchemy level
If onGame == 1
	If Nov == 1			; novice
		PlayerREF.RemoveItemNS MortarPestle 1
		PlayerREF.RemoveItemNS Alembic 1
		PlayerREF.RemoveItemNS Calcinator 1
		PlayerREF.RemoveItemNS Retort 1
	ElseIf App == 1			; apprentice
		PlayerREF.RemoveItemNS MortarPestleApprentice 1
		PlayerREF.RemoveItemNS AlembicApprentice 1
		PlayerREF.RemoveItemNS CalcinatorApprentice 1
		PlayerREF.RemoveItemNS RetortApprentice 1
	ElseIf Jor == 1			; journeyman
		PlayerREF.RemoveItemNS MortarPestleJourneyman 1
		PlayerREF.RemoveItemNS AlembicJourneyman 1
		PlayerREF.RemoveItemNS CalcinatorJourneyman 1
		PlayerREF.RemoveItemNS RetortJourneyman 1
	ElseIf Exr == 1			; expert
		PlayerREF.RemoveItemNS MortarPestleExpert 1
		PlayerREF.RemoveItemNS AlembicExpert 1
		PlayerREF.RemoveItemNS CalcinatorExpert 1
		PlayerREF.RemoveItemNS RetortExpert 1
	ElseIf Mas == 1			; master
		PlayerREF.RemoveItemNS MortarPestleMaster 1
		PlayerREF.RemoveItemNS AlembicMaster 1
		PlayerREF.RemoveItemNS CalcinatorMaster 1
		PlayerREF.RemoveItemNS RetortMaster 1
	EndIf
set onGame to 0		; set to 0 so we can stop the script from running
EndIf
End
