scn IBA000AlchemyMenu

; container ref := ContainerREF
; IBAonGameGlobal is the global we use to stop the quest scripts from running

short menuChose		; this is the variable for the menu option we pick
short btnChoice		; this is the variable for the menu button we pick

; have this block first so GameMode doesn't activate first
Begin OnActivate

	If IBAonGameGlobal != 0 || IBAonGameGlobal == 0
		set IBAonGameGlobal to 1	; set this to 1 since we started the script action
	EndIf

	If menuChose == 0
		set menuChose to -1	; set this value to -1 to initialize
	EndIf
End

Begin GameMode

If IBAonGameGlobal == 1
;--------------------Initial menu--------------------  
	If menuChose == -1
		MessageBox "What do you want to do?", "Deposit all", "Retrieve all", "Retrieve by effect", "View", "Done"
		set menuChose to 1
		set btnChoice to GetButtonPressed
	ElseIf menuChose == 1
		If btnChoice == -1
			set btnChoice to GetButtonPressed
		ElseIf btnChoice == 0
			set menuChose to 10	; deposit all
		ElseIf btnChoice == 1
			set menuChose to 20	; retrieve all
		ElseIf btnChoice == 2
			set menuChose to 25	; retrive by effect
		ElseIf btnChoice == 3
			set menuChose to 30	; view container
		ElseIf btnChoice == 4 
			set menuChose to 2	; done, this is at 2 to not interfere with anything
		EndIf
	EndIf

;--------------------Deposit--------------------  
	If menuChose == 10
		StartQuest IBADepositAllQuest
		set menuChose to 0		; set to 0 since we are done with the menu
		set IBAonGameGlobal to 2	; set to 2 since we are in the second part
	EndIf

;--------------------Retrieve-------------------- 
	If menuChose == 20
		StartQuest IBARetrieveAllQuest
		set menuChose to 0
		set IBAonGameGlobal to 2
	EndIf

;--------------------Retrieve effect--------------------  
	If menuChose == 25
		RetrieveMenuREF.Activate PlayerREF	; activate our container menu
		set menuChose to 0
		set IBAonGameGlobal to 2
	EndIf

;--------------------View--------------------  
	If menuChose == 30
		ContainerREF.Activate PlayerREF	
		set menuChose to 0
		set IBAonGameGlobal to 3		; set this to 3 since they aren't attached to a quest
	EndIf

;--------------------Done--------------------  
	If menuChose == 2
		set menuChose to 0
		set IBAonGameGlobal to 3
	EndIf
EndIf

End
