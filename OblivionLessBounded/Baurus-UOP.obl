scn BaurusScript
; MQ01
; MQ05

short target				; temp var used for chargen
short chargenOver	; set to 1 when chargen is finished so we know to skip last section of script
short sayPlayer		; set to 1 when Baurus first speaks to player
short temp
short warncount		; counts number of times player hits ; goes down over time

begin OnPackageDone MQ05BaurusGoToSewerLowerHall
	if getdistance MQ05BaurusWaitLowerHall < 200
		setstage MQ05 76
	endif
end

begin OnPackageDone CGBaurusToMarker9
	; close the door at the top of the stairs
	CGPrisonEntranceDoor.SetOpenState 0
end

begin onPackageDone CGBaurusSearchAmbushBRoom
	if getstage charactergen < 38
		setstage charactergen 38
	endif
end

begin OnPackageDone CGBaurusToMarkerJ1
	setstage characterGen 67
end

begin OnPackageDone CGBaurusToDeadEmperor
	setstage CharacterGen 85
end

begin OnPackageChange MQ14CheerPlayer
	stoplook
end

begin OnHit
	if getquestrunning charactergen == 1
		; Baurus can't die during charactergen
		set temp to GetBaseActorValue health
		forceav health, temp 
	endif
end

begin OnStartCombat Player
	RETURN
end

begin OnStartCombat
	set warncount to 0

	if glenroyref.isincombat == 0 && getstage charactergen > 40 && getquestrunning charactergen == 1 && (GetStage Charactergen != 80)
		Say Attack
	endif

	if getincell ImperialSewersElvenGardens == 1
		Say MQ00Voice
	endif
end


begin OnHit player
	if getquestrunning charactergen == 1
		if warncount <= 4
			stopcombat
			player.scaonActor
		endif

		SayTo player CharGenBaurus 1
		set warnCount to warnCount + 1

		; make sure disposition of anybody never goes below 30
		; Baurus
		set temp to BaurusRef.getdisposition player
		if temp < 30
			set temp to 30 - temp
			BaurusRef.moddisposition player temp
		endif
		; Renote
		set temp to RenoteRef.getdisposition player
		if temp < 30
			set temp to 30 - temp
			RenoteRef.moddisposition player temp
		endif

		; Emperor
		set temp to UrielSeptimRef.getdisposition player
		if temp < 30
			set temp to 30 - temp
			UrielSeptimRef.moddisposition player temp
		endif

		; Glenroy
		set temp to GlenroyRef.getdisposition player
		if temp < 30
			set temp to 30 - temp
			GlenroyRef.moddisposition player temp
		endif

	endif
end

begin gamemode

if getquestrunning MQ05 == 1
	if getstage MQ05 == 32 && getdistance player < 300
		startconversation player
	endif
endif

; rest of this script is Chargen stuff; ignore once we've finished
if chargenOver == 1
	return
endif

; talk when it is time

if CharacterGen.speaker == 1 && CharacterGen.convTimer <= 0
	set target to CharacterGen.target

	if target == 0
		set CharacterGen.convTimer to Say CharGenMain 1
	elseif target == 1
		set CharacterGen.convTimer to SayTo BaurusRef, CharGenMain 1
	elseif target == 2
		set CharacterGen.convTimer to SayTo RenoteRef, CharGenMain 1
	elseif target == 3
		set CharacterGen.convTimer to SayTo GlenroyRef, CharGenMain 1
	elseif target == 4
		set CharacterGen.convTimer to SayTo UrielSeptimRef, CharGenMain 1
	elseif target == 5
		set CharacterGen.convTimer to SayTo player, CharGenMain 1
	endif	
endif

if getstage charactergen == 19
	if getdistance player < 250 && sayPlayer == 0
		sayTo player CharGenVoice	
		set sayPlayer to 1
	endif
endif

if getstage charactergen >= 18 && getstage charactergen < 22
	if getdistance CGPlayerStartMarker < 200 && CGPlayerCellDoor.getOpenState < 3
		CGPlayerCellDoor.setopenstate 0
		CGPlayerCellDoor.lock 100
		enableplayercontrols
	endif
endif

end


;<CSEBlock>
;<CSECaretPos> 1089 </CSECaretPos>
;</CSEBlock>
